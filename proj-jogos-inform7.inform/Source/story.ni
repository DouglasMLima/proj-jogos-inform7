"Untitled" by Tokyo Drift

Chapter 1 New Kinds

Section 1.1 The Staircase

A staircase is a kind of door. A staircase is usually open. A staircase is seldom openable. A staircase is scenery.
Instead of climbing a staircase: 
	try entering the noun. 

Section 1.2 Chairs

A chair is a kind of supporter that is enterable with carrying capacity 1. 

Definition: A chair is occupied if something is on it.

Understand the command "sit" as something new.
Sitting on is an action applying to one thing.
Understand "sit on [something]" as sitting on.
Understand "sit on top of [something]" as sitting on.

Check an actor sitting on a thing:
	If the noun is occupied, say "You can't sit in the [the noun], it is occupied.[paragraph break]" instead;
	If the noun is not enterable, say "You can't sit on [the noun]![paragraph break]" instead.

Check an actor entering a thing:
	If the thing is occupied, say "You can't sit in a occupied chair" instead;

Carry out sitting on a chair:
	silently try entering the noun.

Report sitting on a chair:
	say "You feel mildly comfortable".
	
Section 1.3 Dealing with animals

Section 1.3.1 Petting animals

Petting is an action applying to one touchable thing.
Understand "pet [something]" as petting.

Carry out petting:
	if the noun is an animal:
		say "You pet [the noun]. You grew closer together!";
	otherwise:
		say "You pet [the noun]. That's kinda weird. Let's hope no one saw you!".

Section 1.3.2 Kissing animals

Instead of kissing spider:
	say "You give [the noun] a little kiss on the butt. [The noun] doesn't react, but you're sure [regarding the noun][they] liked it."

Section 1.3.3 Eating animals

Instead of eating spider:
	say "You could never do that to the cute little spider!"
	
Section 1.3.4 Licking animals

Licking is an action applying to one touchable thing.
Understand "lick [something]" as licking.

Carry out licking:
	if the noun is an animal:
		say "You shudder as you lick [the noun]. [regarding the noun][They] doesn't react.";
	otherwise:
		say "You can't lick [the noun], that'd be gross.".

Chapter 2 Geography

Section 2.1 The House

Porch is a room.  
Hall is room. 

Main door is a door. It is north of Porch and south of Hall. Main door is closed and locked.
The matching key of the Main door is Main key.

Living Room is a room. Living room is north of Hall.
Garage is a room, west of Living Room.
Kitchen is a room, north of garage.
Dining Room is a room, east of kitchen, north of Living Room.
Home Office is a room, east of Living Room.
Lavabo is a room, east of Hall. "The lavabo needs to be cleaned."
Main Stairs is a staircase, above Hall and below Upper Hall.
Upper Hall is a room. 
Large Bedroom is a room, north of Upper Hall.
Medium Bedroom is a room, east of Upper Hall.
Small Bedroom is a room, west of Upper Hall.

Bathroom is a room, south of Upper Hall.
Inner Bathroom is a room, west of Large Bedroom. "The inner bathroom is dirty and damp. There is a sink with a broken mirror above it. A faint light shimmers through a small window above a bathtub."
A mirror, a sink, a bathtub, and a small window are scenery in the inner bathroom.
The description of the mirror is "There are shards of the mirror laying on the sink."
The description of the sink is "The faucet drips very slowly on top of some shards of broken mirror."
The description of the bathtub is "It's a dirty bathtub."
The description of the small window is "It looks like someone could squeeze through there."

Small Window is a staircase, above Inner Bathroom and below Roof 1.
Roof 1 is a room.

First Floor is a region. The Hall, Living Room, Garage, Kitchen, Dining Room, Home Office and Lavabo are in First Floor.
Second Floor is a region. The Upper Hall, The Large Bedroom, the medium bedroom,  The Small Bedroom, The Bathroom and the Inner Bathroom are in Second Floor.

Chapter 3 Things

Player is in Porch.  
Main key is in Porch.

Section 3.1 On the Porch

Wooden chair is a chair. Wooden chair is in Porch.

Old box is a container. Old box is in Porch.

candle is a thing. string is a thing. bucket is a container.
candle, string are in Old box.

bucket is in Porch.

Section 3.2 In the inner bathroom

A spider is a neuter animal in the Inner Bathroom. The description is "A cute little brown spider. It probably won't kill you."

Chapter 4 What Happens when entering

Being Outside the House is a Scene. 
Being Outside the House begins when play begins.
Being Outside the House ends when player is in  Hall.

When Being Outside the House ends:
	Say "The inside smells as bad news...".

Before taking the Main Key during Being Outside the House:
	Say "A chill runs up your spine".

Figure of InitialPicture is the file "house.jpg"
	
When play begins:
	Display the Figure of InitialPicture ;
	Say "A chilling wind blows in the late afternoon".